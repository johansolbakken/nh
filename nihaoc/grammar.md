# Grammar

## LL(1) Parsing
~~~
PROGRAM : GLOBAL_LIST

GLOBAL_LIST : GLOBAL GLOBAL_LIST'
GLOBAL_LIST' : GLOBAL GLOBAL_LIST' | ε

GLOBAL : FUNCTION

FUNCTION : RETURN_TYPE IDENTIFIER L_PAREN ARGUMENT_LIST_OPT R_PAREN STATEMENT

ARGUMENT_LIST_OPT : ARGUMENT_LIST | ε

ARGUMENT_LIST : ARGUMENT ARGUMENT_LIST'

ARGUMENT_LIST' : COMMA ARGUMENT ARGUMENT_LIST' | ε

ARGUMENT : TYPE IDENTIFIER

STATEMENT : BLOCK
          | ASSIGNMENT_STATEMENT SEMICOLON
          | DEFINITION_STATEMENT SEMICOLON
          | FUNCTION_CALL SEMICOLON

ASSIGNMENT_STATEMENT : IDENTIFIER EQUALS EXPRESSION

DEFINITION_STATEMENT : TYPE IDENTIFIER EQUALS EXPRESSION

BLOCK : L_BRACE STATEMENT_LIST R_BRACE

STATEMENT_LIST : STATEMENT STATEMENT_LIST'
STATEMENT_LIST' : STATEMENT STATEMENT_LIST' | ε

RETURN_TYPE : TYPE
            | TYPE_VOID

TYPE : TYPE_INT
     | TYPE_FLOAT
     | TYPE_STRING

EXPRESSION : TERM EXPRESSION'

EXPRESSION' : PLUS TERM EXPRESSION'
            | MINUS TERM EXPRESSION'
            | ε

TERM : FACTOR TERM'

TERM' : STAR FACTOR TERM'
      | SLASH FACTOR TERM'
      | ε

FACTOR : NUMBER
       | IDENTIFIER
       | STRING_DATA
       | FUNCTION_CALL
       | MINUS FACTOR

FUNCTION_CALL : IDENTIFIER L_PAREN EXPRESSION_LIST_OPT R_PAREN

EXPRESSION_LIST_OPT : EXPRESSION_LIST | ε

EXPRESSION_LIST : EXPRESSION EXPRESSION_LIST'

EXPRESSION_LIST' : COMMA EXPRESSION EXPRESSION_LIST' | ε
~~~

## LR(1) Parsing


PROGRAM -> GLOBAL_LIST

GLOBAL_LIST : GLOBAL_LIST GLOBAL | GLOBAL

GLOBAL : FUNCTION
FUNCTION : RETURN_TYPE IDENTIFIER L_PAREN ARGUMENT_LIST R_PAREN STATEMENT
        | RETURN_TYPE IDENTIFIER L_PAREN R_PAREN STATEMENT
STATEMENT : BLOCK
         | ASSIGNMENT_STATEMENT SEMICOLON
         | DEFINITION_STATEMENT SEMICOLON
         | FUNCTION_CALL SEMICOLON

ASSIGNMENT_STATEMENT : IDENTIFIER EQUALS EXPRESSION


DEFINITION_STATEMENT : TYPE IDENTIFIER EQUALS EXPRESSION


BLOCK : L_BRACE STATEMENT_LIST R_BRACE


STATEMENT_LIST : STATEMENT_LIST STATEMENT
              | STATEMENT


RETURN_TYPE : TYPE
            | TYPE_VOID


TYPE : TYPE_INT
    | TYPE_FLOAT
    | TYPE_STRING

ARGUMENT_LIST : ARGUMENT_LIST COMMA TYPE IDENTIFIER
             | TYPE IDENTIFIER

EXPRESSION : NUMBER
      | IDENTIFIER
      | STRING_DATA
      | FUNCTION_CALL
      | EXPRESSION PLUS EXPRESSION
      | EXPRESSION MINUS EXPRESSION
      | EXPRESSION STAR EXPRESSION
      | EXPRESSION SLASH EXPRESSION
      | MINUS EXPRESSION

EXPRESSION_LIST : EXPRESSION_LIST COMMA EXPRESSION
                       | EXPRESSION


FUNCTION_CALL : IDENTIFIER L_PAREN EXPRESSION_LIST R_PAREN
            | IDENTIFIER L_PAREN R_PAREN


